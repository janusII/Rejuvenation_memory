ColorNames = "c1 c2 c3 c4 c5 c6 c7 c8 c9 c10 c11 c12 g0_1 g0_2 g0_3 g0_4 g1_1 g1_2 g1_3 g1_4"
ColorValues = "0xe69f00 0x56b4e9 0x009e73 0xcc79a7 0x0072b2 0xd55e00 0x9400d3 0xf0e442 0x999999 0x000000 0x97D8C4 0XF30DE5 0xF5B7B1 0xEC7063 0xCB4335 0x943126 0xAED6F1 0x5DADE2 0x2E86C1 0x21618C"

myColor(c) = (idx=NaN, sum [i=1:words(ColorNames)] (c eq word(ColorNames,i) ? idx=i : idx), word(ColorValues,idx))
# add transparency (alpha) a=0 to 255 or 0x00 to 0xff
myTColor(c,a) = sprintf("0x%x%s",a, myColor(c)[3:])


#######################################################################
################### FIGURA ARTICULO DOS PANELES #######################
#######################################################################

reset
set auto

set terminal epslatex size 6,9 color colortext 16 font ",17" standalone header "\\usepackage{bm}"
set output "fig4.tex"

LEFT = 0.13
RIGHT = 0.99
BOTTOM = 0.06
TOP = 0.99

DY=TOP-BOTTOM


set multiplot

set tmargin at screen TOP
set bmargin at screen BOTTOM+DY/2
set lmargin at screen LEFT
set rmargin at screen RIGHT

set logs y
set key at 0.63,0.4
set key samplen -1 spacing 1.2
set ylabel "$F(\\tilde{X})$" offset 4.3,0
set format x ' '
unset xlabel
set xrange [0.45:1]
set label "$\\bm{T=0.5}$" at 0.50,0.6 font ",24"
set ytics offset 0.9,-0.1
set xtics offset 0,0.4

p "<./lector_dist_func_errors ../../data/fig4/sg/T05/dist_func_nrinfty_r05_009837f051_0000000400.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c1") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T05/dist_func_nrinfty_r05_009837f051_000000c567.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c2") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T05/dist_func_nrinfty_r05_009837f051_0000b504f3.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c3") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T05/dist_func_nrinfty_r05_009837f051_009837f051.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c4") not,\
"<./lector_dist_func_errors ../../data/fig4/isdil/T0509/dist_func_nrinfty_r05_000000004c_00000001ae.bin" u 1:2:3 w xerrorbars pt 4 lc rgb myColor("c10") not,\
"<./lector_dist_func_errors ../../data/fig4/isdil/T0505/dist_func_nrinfty_r05_000000004c_0000000045.bin" u 1:2:3 w xerrorbars pt 4 lc rgb myColor("c6") not ,\
0/0 with points ps 2 pt 7 lc rgb myColor("c1") t "SG: $(\\mathrm{A_{5}}$,$\\mathrm{B_{5}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c2") t "SG: $(\\mathrm{A_{5}}$,$\\mathrm{C_{5}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c3") t "SG: $(\\mathrm{A_{5}}$,$\\mathrm{D_{5}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c4") t "SG: $(\\mathrm{A_{5}}$,$\\mathrm{E_{5}})$",\
0/0 with points ps 2 pt 4 lc rgb myColor("c10") t "DIM: $(\\mathrm{A'_{5}}$,$\\mathrm{A'_{9}})$",\
0/0 with points ps 2 pt 4 lc rgb myColor("c6") t "DIM: $(\\mathrm{A'_{5}}$,$\\mathrm{B'_{5}})$"   


set tmargin at screen TOP-DY/2
set bmargin at screen BOTTOM
set lmargin at screen LEFT
set rmargin at screen RIGHT

set logs y
set key at 0.63,0.4
set key samplen -1 spacing 1.2
set xlabel "$\\tilde{X}$" offset 0,0.8
set ylabel "$F(\\tilde{X})$" offset 4.3,0
set format x '%g'
set xrange [0.45:1]
unset label
set label "$\\bm{T=0.7}$" at 0.50,0.6 font "Helvetica,24"
 
p "<./lector_dist_func_errors ../../data/fig4/sg/T07/dist_func_nrinfty_r05_009837f051_0000000400.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c1") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T07/dist_func_nrinfty_r05_009837f051_000000c567.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c2") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T07/dist_func_nrinfty_r05_009837f051_0000b504f3.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c3") not,\
"<./lector_dist_func_errors ../../data/fig4/sg/T07/dist_func_nrinfty_r05_009837f051_009837f051.bin" u 1:2:3 w xerrorbars pt 7 lc rgb myColor("c4") not,\
"<./lector_dist_func_errors ../../data/fig4/isdil/T0709/dist_func_nrinfty_r05_00000000c5_00000001ae.bin" u 1:2:3 w xerrorbars pt 4 lc rgb myColor("c10") not,\
"<./lector_dist_func_errors ../../data/fig4/isdil/T0707/dist_func_nrinfty_r05_00000000c5_00000000a5.bin" u 1:2:3 w xerrorbars pt 4 lc rgb myColor("c6") not,\
0/0 with points ps 2 pt 7 lc rgb myColor("c1") t "SG: $(\\mathrm{A_{7}}$,$\\mathrm{B_{7}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c2") t "SG: $(\\mathrm{A_{7}}$,$\\mathrm{C_{7}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c3") t "SG: $(\\mathrm{A_{7}}$,$\\mathrm{D_{7}})$",\
0/0 with points ps 2 pt 7 lc rgb myColor("c4") t "SG: $(\\mathrm{A_{7}}$,$\\mathrm{E_{7}})$",\
0/0 with points ps 2 pt 4 lc rgb myColor("c10") t "DIM: $(\\mathrm{A'_{7}}$,$\\mathrm{A'_{9}})$",\
0/0 with points ps 2 pt 4 lc rgb myColor("c6") t "DIM: $(\\mathrm{A'_{7}}$,$\\mathrm{B'_{7}})$"

unset multiplot
